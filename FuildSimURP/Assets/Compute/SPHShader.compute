#pragma kernel ComputeDensity
#pragma kernel ComputeExternal
#pragma kernel ComputePressure
#pragma kernel ComputeViscosity
#pragma kernel Intergrate
#pragma kernel CollisionDetection

static const int NumThreads = 64;
static const float PI = 3.14159265f;

//buffers
RWStructuredBuffer<float3> positions;
RWStructuredBuffer<float3> velocities;
RWStructuredBuffer<float3> forces;
RWStructuredBuffer<float> densities;
RWStructuredBuffer<float> pressures;

RWStructuredBuffer<float3> collisionSpheres;

// variables
const float3 gravity;
const float restDensity;
const float gasConstant;
const float kernalRadius;
const float mass;
const float viscosityConst;
const float timeStep;
const float boundaryDamping;
const float3 view;

const float interactionRadius;
const float3 interactionPoint1;
const float3 interactionPoint2;

const float POLY6;
const float SPIKYGRAD;
const float VISCLAP;

const float collisionSphereRadius;
const uint collisionSphereCount;

const uint numParticles;

double squareRadius()
{
    return (double)kernalRadius * (double)kernalRadius;
}

[numthreads(NumThreads, 1, 1)]
void ComputeDensity(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;

    double tempValue = 0.001;
    for (uint i = 0; i < numParticles; i++)
    {
        double3 dir = positions[i] - positions[id.x];
        double squaredNorm = dir.x * dir.x + dir.y * dir.y + dir.z * dir.z;

        if (squaredNorm < squareRadius())
        {
            tempValue += mass * POLY6 * pow(squareRadius() - squaredNorm, 3.0);
        }
    }
    densities[id.x] = (float)tempValue;
}

[numthreads(NumThreads, 1, 1)]
void ComputeExternal(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;
    double3 externalForce = gravity * (mass / densities[id.x]);
    if (densities[id.x] != 0)
    {
        double3 dir = interactionPoint1 - positions[id.x];
        if (dot(dir, dir) < interactionRadius * interactionRadius)
            externalForce = dir * (mass / densities[id.x]);
    }
    if (densities[id.x] != 0)
    {
        double3 dir = interactionPoint2 - positions[id.x];
        if (dot(dir, dir) < interactionRadius * interactionRadius)
            externalForce = -dir * (mass / densities[id.x]);
    }
    forces[id.x] += (float3)externalForce;
}

[numthreads(NumThreads, 1, 1)]
void ComputePressure(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;

    pressures[id.x] = gasConstant * (densities[id.x] - restDensity);

    double3 forcePressure = double3(0, 0, 0);
    for (uint i = 0; i < numParticles; i++)
    {
        if (id.x == i)
            continue;

        double3 dir = positions[i] - positions[id.x];
        double norm = sqrt(dir.x * dir.x + dir.y * dir.y + dir.z * dir.z);
        
        if (norm < kernalRadius)
        {
            forcePressure += -normalize(dir) * mass * 
                ((pressures[id.x] + pressures[i]) / (2.0f * densities[i])) *
                SPIKYGRAD * pow(kernalRadius - norm, 3.0f);
        }
    }
    forces[id.x] += (float3)forcePressure;
}

[numthreads(NumThreads, 1, 1)]
void ComputeViscosity(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;

    double3 forceViscosity = double3(0, 0, 0);

    for (uint i = 0; i < numParticles; i++)
    {
        if (id.x == i)
            continue;

        double3 dir = positions[i] - positions[id.x];
        double norm = sqrt(dir.x * dir.x + dir.y * dir.y + dir.z * dir.z);
        if (norm < kernalRadius)
        {
            forceViscosity += viscosityConst * mass * 
                ((velocities[i] - velocities[id.x]) / densities[i]) * 
                VISCLAP * (kernalRadius - norm);
        }
    }
    forces[id.x] += (float3)forceViscosity;
}

[numthreads(NumThreads, 1, 1)]
void Intergrate(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;

    velocities[id.x] += timeStep * (forces[id.x] / densities[id.x]);
    forces[id.x] = 0;
    positions[id.x] += timeStep * velocities[id.x];

    if (positions[id.x].x - kernalRadius < 0.0f)
    {
        velocities[id.x].x *= boundaryDamping;
        positions[id.x].x = kernalRadius;
    }
    if (positions[id.x].x + kernalRadius > view.x)
    {
        velocities[id.x].x *= boundaryDamping;
        positions[id.x].x = view.x - kernalRadius;
    }

    if (positions[id.x].y - kernalRadius < 0.0f)
    {
        velocities[id.x].y *= boundaryDamping;
        positions[id.x].y = kernalRadius;
    }
    if (positions[id.x].y + kernalRadius > view.y)
    {
        velocities[id.x].y *= boundaryDamping;
        positions[id.x].y = view.y - kernalRadius;
    }
    
    if (positions[id.x].z - kernalRadius < 0.0f)
    {
        velocities[id.x].z *= boundaryDamping;
        positions[id.x].z = kernalRadius;
    }
    if (positions[id.x].z + kernalRadius > view.z)
    {
        velocities[id.x].z *= boundaryDamping;
        positions[id.x].z = view.z - kernalRadius;
    }

}

bool SphereSphereIntersection(float3 pos1, float3 pos2)
{
    float3 dist = pos1 - pos2;
    float length = sqrt(dist.x * dist.x + dist.y * dist.y + dist.z * dist.z);
    float sumRadius = kernalRadius + collisionSphereRadius;
    if (length <= sumRadius)
        return true;
    return false;
}

[numthreads(NumThreads, 1, 1)]
void CollisionDetection(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;

    for (int i = 0; i < collisionSphereCount; i++)
    {
        if (SphereSphereIntersection(positions[id.x], collisionSpheres[i]))
        {
            float3 xDir = positions[id.x] - collisionSpheres[i];
            float3 normX = float3(xDir.x / 3, xDir.y / 3, xDir.z / 3);
            float xdotv = dot(normX, velocities[id.x]);
            float3 veloX = normX * xdotv;
            float3 veloY = velocities[id.x] - veloX;
            xDir *= -1;
            float3 xdotv2 = float3(xDir.x / 3, xDir.y / 3, xDir.z / 3);
            float3 veloX2 = xDir * xdotv2;
            
            velocities[id.x] = (veloX * (mass - 999) / (mass + 999))
                + (veloX2 * (2 * 999) / (mass + 999)) + veloY;
        }
    }

}