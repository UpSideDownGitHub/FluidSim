#pragma kernel ComputeDensity
#pragma kernel ComputeExternal
#pragma kernel ComputePressure
#pragma kernel ComputeViscosity
#pragma kernel Intergrate

static const int NumThreads = 64;
static const float PI = 3.14159265f;

//buffers
RWStructuredBuffer<float2> positions;
RWStructuredBuffer<float2> velocities;
RWStructuredBuffer<float2> forces;
RWStructuredBuffer<float> densities;
RWStructuredBuffer<float> pressures;

// variables
const float2 gravity;
const float restDensity;
const float gasConstant;
const float kernalRadius;
const float mass;
const float viscosityConst;
const float timeStep;
const float boundaryDamping;
const float2 view;

const float interactionRadius;
const float interaction1;
const float interaction2;
const float2 interactionPoint;

const float POLY6;
const float SPIKYGRAD;
const float VISCLAP;

const uint numParticles;

double squareRadius()
{
    return (double)kernalRadius * (double)kernalRadius;
}

[numthreads(NumThreads, 1, 1)]
void ComputeDensity(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;

    double tempValue = 0.001;
    for (uint i = 0; i < numParticles; i++)
    {
        double2 dir = positions[i] - positions[id.x];
        double squaredNorm = dir.x * dir.x + dir.y * dir.y;

        if (squaredNorm < squareRadius())
        {
            tempValue += mass * POLY6 * pow(squareRadius() - squaredNorm, 3.0);
        }
    }
    densities[id.x] = (float)tempValue;
}

[numthreads(NumThreads, 1, 1)]
void ComputeExternal(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;
    float2 externalForce = gravity * (mass / densities[id.x]);
    if (interaction1 != 0 && densities[id.x] != 0)
    {
        float2 dir = interactionPoint - positions[id.x];
        if (dot(dir, dir) < interactionRadius * interactionRadius)
            externalForce = dir * (mass / densities[id.x]);
    }
    else if (interaction2 != 0 && densities[id.x] != 0)
    {
        float2 dir = interactionPoint - positions[id.x];
        if (dot(dir, dir) < interactionRadius * interactionRadius)
            externalForce = -dir * (mass / densities[id.x]);
    }
    forces[id.x] += externalForce;
}

[numthreads(NumThreads, 1, 1)]
void ComputePressure(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;

    pressures[id.x] = gasConstant * (densities[id.x] - restDensity);

    double2 forcePressure = double2(0, 0);
    for (uint i = 0; i < numParticles; i++)
    {
        if (id.x == i)
            continue;

        double2 dir = positions[i] - positions[id.x];
        double norm = sqrt(dir.x * dir.x + dir.y * dir.y);
        
        if (norm < kernalRadius)
        {
            forcePressure += -normalize(dir) * mass * 
                ((pressures[id.x] + pressures[i]) / (2.0f * densities[i])) *
                SPIKYGRAD * pow(kernalRadius - norm, 3.0f);
        }
    }
    forces[id.x] += (float2)forcePressure;
}

[numthreads(NumThreads, 1, 1)]
void ComputeViscosity(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;

    double2 forceViscosity = double2(0, 0);

    for (uint i = 0; i < numParticles; i++)
    {
        if (id.x == i)
            continue;

        float2 dir = positions[i] - positions[id.x];
        double norm = sqrt(dir.x * dir.x + dir.y * dir.y);
        if (norm < kernalRadius)
        {
            forceViscosity += viscosityConst * mass * 
                ((velocities[i] - velocities[id.x]) / densities[i]) * 
                VISCLAP * (kernalRadius - norm);
        }
    }
    forces[id.x] += (float2)forceViscosity;
}

[numthreads(NumThreads, 1, 1)]
void Intergrate(uint3 id : SV_DispatchThreadID)
{
    if (id.x >= numParticles)
        return;

    velocities[id.x] += timeStep * forces[id.x] / densities[id.x];
    forces[id.x] = 0;
    positions[id.x] += timeStep * velocities[id.x];

    if (positions[id.x].x - kernalRadius < 0.0f)
    {
        velocities[id.x].x *= boundaryDamping;
        positions[id.x].x = kernalRadius;
    }
    if (positions[id.x].x + kernalRadius > view.x)
    {
        velocities[id.x].x *= boundaryDamping;
        positions[id.x].x = view.x - kernalRadius;
    }

    if (positions[id.x].y - kernalRadius < 0.0f)
    {
        velocities[id.x].y *= boundaryDamping;
        positions[id.x].y = kernalRadius;
    }
    if (positions[id.x].y + kernalRadius > view.y)
    {
        velocities[id.x].y *= boundaryDamping;
        positions[id.x].y = view.y - kernalRadius;
    }
}